;;;; SPDX-FileCopyrightText: hu.dwim & Atlas Engineer LLC
;;;; SPDX-License-Identifier: Public Domain

(in-package :nclass/test)

(defun with-test-class-options (thunk)
  "A context sets the class options to specific defaults."
  (let ((*package* #.*package*)
        (nclass::*automatic-accessors-p* t)
        (nclass::*accessor-name-transformer* 'default-accessor-name-transformer)
        (nclass::*automatic-initargs-p* t)
        (nclass::*initarg-name-transformer* 'default-initarg-name-transformer)
        (nclass::*export-class-name-p* nil)
        (nclass::*export-accessor-names-p* nil)
        (nclass::*export-slot-names-p* nil)
        (nclass::*automatic-predicates-p* nil))
    (funcall thunk)))

(defmacro assert-slot= (expansion form &rest extras)
  `(let ((nclass::*accessor-names* nil)
         (nclass::*slot-names* nil))
     (assert-equal
      ',expansion
      (nclass::process-slot-definition ',form)
      ,@extras)))

(defmacro assert-slot-warns (form &rest extras)
  `(let ((nclass::*accessor-names* nil)
         (nclass::*slot-names* nil))
     (assert-warning 'warning
                     (nclass::process-slot-definition ',form) ,@extras)))

(defmacro assert-slot-errors (form &rest extras)
  `(let ((nclass::*accessor-names* nil)
         (nclass::*slot-names* nil))
     (assert-error 'error
                   (nclass::process-slot-definition ',form) ,@extras)))

(define-test nop (:contexts '(with-test-class-options))
  (assert-expands
   (defclass some-class (some super classes)
     ()
     (1 2)
     (3 4))
   (defclass* some-class (some super classes)
       ()
       (1 2)
       (3 4))))

(define-test accessors (:contexts '(with-test-class-options))
  (assert-slot= (slot1 :accessor slot1-custom :initarg slot1-custom)
                (slot1 :unbound :accessor slot1-custom :initarg slot1-custom))
  (assert-slot= (slot1 :accessor slot1-of :initarg :slot1)
                (slot1))
  (assert-slot= (slot1 :accessor slot1-p :initarg :slot1 :type boolean)
                (slot1 :unbound :type boolean))
  (assert-slot= (slotp :accessor slotp :initarg :slotp :type boolean)
                (slotp :unbound :type boolean))
  (assert-slot= (slot-name :accessor slot-name-p :initarg :slot-name :type boolean)
                (slot-name :unbound :type boolean))
  (let ((nclass::*automatic-accessors-p* nil)
        (nclass::*automatic-initargs-p* nil))
    (assert-slot= (slot1)
                  (slot1)))
  (let ((nclass::*accessor-name-transformer* (make-name-transformer "FOO-" name "-BAR"))
        (nclass::*initarg-name-transformer* (make-name-transformer "BAZ-" name)))
    (assert-slot= (slot1 :accessor foo-slot1-bar :initarg baz-slot1)
                  (slot1))))

(define-test reconfiguration (:contexts '(with-test-class-options))
  (assert-expands (defclass some-class (some super classes)
                    ((slot1 :accessor slot1-zork :initarg :slot1))
                    (1 2)
                    (3 4))
                  (defclass* some-class (some super classes)
                      ((slot1))
                      (1 2)
                      (:accessor-name-transformer (make-name-transformer name "-ZORK"))
                      (3 4)))
  (assert-expands (defclass some-class (some super classes)
                    ((slot1 :accessor nclass/test.dummy::slot1-of :initarg :slot1)))
                  (defclass* some-class (some super classes)
                      ((slot1))
                      (:accessor-name-package (find-package :nclass/test.dummy)))))

(define-test full (:contexts '(with-test-class-options))
  (assert-expands (defclass some-class (some super classes)
                    ((slot1 :accessor slot1-of :initarg :slot1 :documentation "zork"))
                    (1 2)
                    (3 4))
                  (defclass* some-class (some super classes)
                      ((slot1 :unbound :documentation "zork"))
                      (1 2)
                      (3 4)))
  (assert-expands (defclass some-class (some super classes)
                    ((slot1 :initform 42 :documentation "zork"))
                    (1 2)
                    (3 4))
                  (defclass* some-class (some super classes)
                      ((slot1 42 :documentation "zork"))
                      (1 2)
                      (:automatic-accessors-p nil)
                      (:automatic-initargs-p nil)
                      (3 4)))
  (assert-equal `(progn
                  (defclass some-class (some super classes)
                    ((slot1 :initform 42 :accessor slot1-of :initarg :slot1 :documentation "zork")
                     (slot2 :accessor slot2-custom :initarg :slot2)
                     (slot3 :initarg :slot3)))
                  (eval-when (:compile-toplevel :load-toplevel :execute)
                    (export '(some-class slot2 slot2-custom slot1 slot1-of) ,(package-name *package*)))
                  (find-class 'some-class nil))
                (macroexpand-1 '(defclass* some-class (some super classes)
                                    ((slot1 42 :documentation "zork")
                                     (slot2 :unbound :accessor slot2-custom)
                                     (slot3 :unbound :accessor nil :export :accessor))
                                    (:export-accessor-names-p t)
                                    (:export-class-name-p t)
                                    (:export-slot-names-p t))))
  (assert-equal `(progn
                   (defclass some-class (some super classes)
                     ((slot1 :initform 42 :accessor slot1-of :initarg :slot1 :documentation "zork")
                      (slot2 :accessor slot2-custom :initarg :slot2)
                      (slot3 :initform 42 :accessor slot3-of :initarg :slot3)
                      (slot4 :initform 42 :accessor slot4-of :initarg :slot4)))
                   (eval-when (:compile-toplevel :load-toplevel :execute)
                     (export '(some-class slot3-of slot2 slot2-custom slot1) ,(package-name *package*)))
                   (find-class 'some-class nil))
                (macroexpand-1 '(defclass* some-class (some super classes)
                                 ((slot1 42 :documentation "zork" :export :slot)
                                  (slot2 :unbound :accessor slot2-custom)
                                  (slot3 42 :export :accessor)
                                  (slot4 42 :export nil))
                                 (:export-accessor-names-p t)
                                 (:export-class-name-p t)
                                 (:export-slot-names-p t)))))

(define-test warnings-and-errors (:contexts '(with-test-class-options))
  (let ((*allowed-slot-definition-properties* *allowed-slot-definition-properties*))
    (push :asdf *allowed-slot-definition-properties*)
    (push :unspecified *allowed-slot-definition-properties*)
    (assert-slot= (slot1 :accessor slot1-of :initarg :slot1 :asdf 42 :unspecified 43)
                  (slot1 :asdf 42 :unspecified 43)))
  (assert-slot-warns (slot1 :unbound :asdf slot1-custom))
  (assert-slot-warns (slot1 :asdf slot1-custom))
  (assert-slot-errors (slot1 :unbound foo bar))
  (assert-slot-errors (slot1 foo bar))

  (assert-error 'error
                (macroexpand-1 '(defclass* some-class ()
                                 ((slot1))
                                 (:accessor-name-transformer 'default-accessor-name-transformer many)))))

(define-test skip-export-slot-name-from-foreign-package (:contexts '(with-test-class-options))
  (let* ((pkg1 (find-package :nclass/test.pkg1))
         (pkg2 (find-package :nclass/test.pkg2))
         (exp1 (let ((*package* pkg1))
                 (macroexpand-1
                  '(defclass* nclass/test.pkg1::foo ()
                    ((nclass/test.pkg1::foo-name))
                    (:export-slot-names-p nil)))))
         (exp2 (let ((*package* pkg2))
                 (macroexpand-1
                  '(defclass* nclass/test.pkg2::bar
                    (nclass/test.pkg1::foo)
                    ;; override the slot in the superclass
                    ((nclass/test.pkg1::foo-name :initform ""))
                    (:export-slot-names-p t))))))
    (let ((*package* pkg1))
      (eval exp1))
    (let ((*package* pkg2))
      (eval exp2))
    (assert-eq :internal
               (nth-value 1 (find-symbol "FOO-NAME" :nclass/test.pkg1)))
    ;; REVIEW: Is the following the right way to re-initialize the packages?
    (delete-package pkg1)
    (delete-package pkg2)
    (defpackage :nclass/test.pkg1
      (:use :nclass))
    (defpackage :nclass/test.pkg2
      (:use :nclass))))
